title: Room0
tags:
color: white
position: -53,-51
---
Nanditha: Oh, I'm so excited. Thank you Ashok, this is an awesome birthday present!
Hugh: Calm down, it hasn't even started yet.
Mathilda: And this room is not exactly thrilling. Also, I want my phone back!
Ashok: I guess they have to set up the next room first... And it's more immersive without phones, don't you think?
Nanditha: Hey Marzanna, are you alright?
Marzanna: It's so dark in here.
Hugh: Who had the brilliant idea to invite this coward?
Mathilda: Well, it's funnier if someone is actually afraid.
Nanditha: Hey, don't be so harsh to her! I am glad that you are here, Marzanna.

-> Play the sounds
    *noise of a struggle and a scream*
    // noise of a struggle and a scream
    Marzanna: Uuuuh. I hate Escape Rooms.
    Ashok: I think it's starting!
<<jump Room1_traps>>
===

title: Room1_traps
tags:
group: R1
color: orange
position: -22,152
---
Set trap?
<<if visited_count("Room1_enter") < 2>>
-> Yes
    -> Chemicals Shelf
        <<set $trapchemicals1 = true>>
    -> Taxidermy Tools
        <<set $trapknives1 = true>>
-> No
<<elseif visited_count("Room1_enter") > 3>>
-> Yes
    -> Corpse
-> No
<<else>>
-> Yes
    -> Workbench
    -> Stuffed Animals
-> No
<<endif>>
<<jump Room1_enter>>
===

title: Room1_enter
tags:
group: R1
color: orange
position: -18,314
---
-> Open the door
*a door in the wooden crate opens, the characters enter the room*
<<if visited_count("Room1_enter") == 0>>
Mathilda: Oh wow, this looks pretty cool. And realistic.
Hugh: Glad it doesn't smell realistic.
Nanditha: Ok, let's find clues and get out of here.
// Characters scatter
<<elseif visited_count("Room1_enter") == 1>>
Hugh: What the hell?
Mathilda: How is this possible?
Nanditha: Let's go back, maybe we took the wrong door.
Marzanna: But there was no other door!
<<wait  2>>
Ashok: It won't open...
Mathilda: Maybe it's just stuck?
Hugh: Let me try.
<<wait  2>>
Hugh: No, it's actually locked.
Marzanna: We're gonna die in here!
<<endif>>
<<jump Room1_choice>>
===

title: Room1_choice
tags:
group: R1
color: orange
position: -21,517
---
<<if $Mathildastabbed1 == true>>
-> Chemicals Shelf//Marzanna inspects the bottles
    <<jump Room1_chemicals1>>
-> Stuffed Animals//Nanditha inspects the animals
    <<jump Room1_animals1>>
-> Bloody Corpse//Ashok inspects the corpse and trail
    <<jump Room1_corpse1>>
<<elseif $Marzannaburned1 == true>>
-> Wooden Crate//Hugh inspects the shipping labels
    <<jump Room1_crate1>>
-> Taxidermy Tools//Mathilda inspects the knives
    <<jump Room1_knives1>>
-> Stuffed Animals//Nanditha inspects the animals
    <<jump Room1_animals1>>
<<else>>
-> Wooden Crate//Hugh inspects the shipping labels
    <<jump Room1_crate1>>
-> Taxidermy Tools//Mathilda inspects the knives
    <<jump Room1_knives1>>
-> Chemicals Shelf//Marzanna inspects the bottles
    <<jump Room1_chemicals1>>
-> Stuffed Animals//Nanditha inspects the animals
    <<jump Room1_animals1>>
-> Bloody Corpse//Ashok inspects the corpse and trail
    <<jump Room1_corpse1>>
<<endif>>
===

title: Room1_crate1
tags:
group: R1
color: orange
position: 363,659
---
Hugh: Looks like we were some kind of delivery. This crate we just came out of has a stamp that says "fresh meat".
Reveal Key?
    -> Yes
        Hugh: There's something behind the label... Oh I guess I found the key for the door over there. Let's see...
        //They gather at the door, it opens
        Mathilda: That was way too easy.
        <<jump Room2_enter>>
    -> No
        <<jump Room1_choice>>
===

title: Room1_knives1
tags:
group: R1
color: orange
position: 365,807
---
<<if $trapknives1 == true>>
//knives fall from the wall and injure Mathilda
Mathilda: Aaaaaah!
//everyone gathers around Mathilda
<<set $Mathildastabbed1 = true>>
Hugh: Mathilda!
Nanditha: Tilly! Oh my god, what happened? Your arm!
Mathilda: These fucking knives fell from this stupid board when I touched it and they stabbed me! Schei√üe!!
Hugh: What the fuck. Hey Ashok what a shit place did you pick?!
Ashok: Hey! Let us out! There has been an accident! Hello! Someone is injured.
Mathilda: Don't touch me! This hurts like hell!
Nanditha: Sorry.
Hugh: We need to stop the bleeding. //pulls his belt and attaches it to Mathilda's arm
Ashok: Can you hear me? Let us out!
Marzanna: They won't let us out. We are trapped. For real!
Ashok: No way, they surely just need some time to unlock the doors.
<<wait  3>>
Marzanna: Oh noooo I was right.
Mathilda: Wollt ihr mich verarschen?!? Fick dich Ashok!
Nanditha: Everyone, stay calm! We need to find a way out and call an ambulance.
//everyone returns to their posts, searching for the key. Hugh stays with Mathilda.
Hugh: Don't worry, it's not that bad. //Hugh takes a knife
 <<set $Hughknife = true>>
<<else>>
Mathilda: Maybe we will need a weapon to fight the animal.
Ashok: Seriously?! This is just a game. Those things are probably not even real.
Offer Knife?
    -> Yes
        <<set $Mathildaknife = true>>
        Mathilda: This one is loose and that means we are supposed to take it, doesn't it?
        Ashok: Or they just forgot to glue it to the wall.
        Mathilda: I'll take it with me.
    -> No
<<endif>>
<<jump Room1_choice>>
===

title: Room1_chemicals1
tags:
group: R1
color: orange
position: 361,357
---
<<if $trapchemicals1 == true>>
//Marzanna gets chemical burn.
<<set $Marzannaburned1 = true>>
Marzanna: Iiiiiek!
<<jump Room1_choice>>
<<else>>
Marzanna: Do you think these bottles actually contain what's written on them? 
Hugh: No way, it's just dyed water for shure.
Offer Arsenic Trioxide?
    -> Yes
        <<set $Marzannapoison = true>>
    -> No
<<jump Room1_choice>>
<<endif>>
===

title: Room1_animals1
tags:
group: R1
color: orange
position: 360,216
---
Nanditha: They look creepy and cute at the same time. Weird.
Reveal Key?
    -> Yes
        Nanditha: What's that? Oh, I found a key! Yay!
        Ashok: Let's try it.
        //They gather at the door, it opens
        <<jump Room2_enter>>
    -> No
        <<jump Room1_choice>>
===

title: Room1_corpse1
tags:
group: R1
color: orange
position: 356,505
---
Ashok: Wow, it looks quite real... This trail leads to the door. They are too smudged to be shure but I think it's paw prints.
Hugh: How would a human rip someone apart like this?
Ashok: You got a point there.
Slip Note?
    -> Yes
        Ashok: I think I found some kind of hint. There was a piece of paper next to the body.
        Nanditha: What does it say?
        Ashok: "No way out."
        <<if $Mathildastabbed1 == true>>
            Mathilda: Is this supposed to be a joke?
            Hugh: It's not funny. Let's 
        <<elseif $Marzannaburned1 == true>>
            Marzanna: We're going to die in here!
        <<else>>
            Marzanna: Oh no.
            Hugh: Calm down, don't tell me you get scared by three random words already?
        <<endif>>
    -> No
<<jump Room1_choice>>
Reveal Key?
    -> Yes
        Ashok: Wait, maybe it wasn't an animal. I found the murder weapon!
        <<wait  1>>
        Ashok: Oh, it's a key.
        Mathilda: How do you kill someone with a key?
        Nanditha: Let's try to open the door.
        //They gather at the door, it opens
        <<jump Room2_enter>>
    -> No
        <<jump Room1_choice>>
===

title: Room2_enter
tag:
group: R2
color: green
position: 739,163
---
You've reached the end of this demo.
<<jump Room1_enter>>
===